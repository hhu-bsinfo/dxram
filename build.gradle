buildscript {
    repositories {
        mavenCentral()

        maven {
            url "https://plugins.gradle.org/m2/"
        }

        maven {
            url "https://dl.bintray.com/hhu-bsinfo/dxram"
        }
    }

    dependencies {
        classpath "de.hhu.bsinfo:dxgradle:1.0.12"
    }
}

plugins {
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

apply plugin: 'dxgradle'
group = 'de.hhu.bsinfo'
version = '0.3.1-dev'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

mainClassName = 'de.hhu.bsinfo.dxram.DXRAM'

// There doesn't seem to be a way get the APP_HOME variable to the path of config files relative
// to the application dir. Source:
// https://stackoverflow.com/questions/25227198/how-to-pass-a-reference-to-distribution-home-directory-using-gradle-application
project.startScripts.with {
    doLast {
        unixScript.text = unixScript.text.replaceFirst('(?<=DEFAULT_JVM_OPTS=)((\'|\")(.*)(\'|"))(?=\n)',
                '\'$3 "-Dlog4j.configurationFile=file:\\$APP_HOME/config/log4j2.xml ' +
                        '-Ddxram.config=$APP_HOME/config/dxram.json"\'')
    }
}

repositories {
    mavenCentral()

    maven {
        url "https://dl.bintray.com/hhu-bsinfo/dxram"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Werror", "-Xlint:sunapi", "-XDenableSunApiLintControl"]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

dependencies {
    implementation fileTree(dir: 'lib', include: ['*.jar'])

    implementation 'de.hhu.bsinfo:dxnet:1.0.0'
    implementation 'de.hhu.bsinfo:dxmon:1.0.0'
    nativeImplementation 'de.hhu.bsinfo:dxutils:1.0.0'

    implementation 'org.apache.zookeeper:zookeeper:3.4.3'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.9.0'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

apply from: 'publish.gradle'
apply from: 'types.gradle'

task wrapper(type: Wrapper) {
    gradleVersion = "4.9"
}
